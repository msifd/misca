buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public' }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'idea'

version = project.mod_version
group = 'msifeed.misca'
archivesBaseName = 'misca'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: 'stable', version: '39-1.12'

    accessTransformer = file('src/main/resources/ts_at.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')
            args "--tweakClass", "org.spongepowered.asm.launch.MixinTweaker",
                    "--mixin", "mixins.misca.json",
                    "--username", "msifeed"
            jvmArgs "-Dfml.coreMods.load=msifeed.misca.mixins.MiscaDepsPlugin"

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }

        server {
            args "--tweakClass", "org.spongepowered.asm.launch.MixinTweaker",
                    "--mixin", "mixins.misca.json"
            jvmArgs "-Dfml.coreMods.load=msifeed.misca.mixins.MiscaDepsPlugin"

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

repositories {
    maven { url 'https://repo.spongepowered.org/repository/maven-public' }
    maven { url 'https://www.cursemaven.com' }
    maven { url 'https://dvs1.progwml6.com/files/maven' }
    maven { url 'https://maven.enginehub.org/repo/' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2859'

    embed 'org.spongepowered:mixin:0.8.3'
    embed 'org.mariadb.jdbc:mariadb-java-client:2.7.2'
    embed 'org.mongodb:mongodb-driver-sync:4.2.2'

    compile files('libs/customstuff4-1.12.2-0.6.9a-deobf.jar')
    compile "curse.maven:techguns-244201:2958103"
    compile "curse.maven:baubles-227083:2518667"
    compile "curse.maven:flans-mod-5-5-2-317310:3195600"
    compile "curse.maven:electroblobs-wizardry-265642:3189062"
    compile "curse.maven:thaumcraft-223628:2629023"
    compile "curse.maven:athenaeum-284350:3063275"
    compile "curse.maven:artisan-worktables-284351:3205284"
    compile "slimeknights.mantle:Mantle:1.12-1.3.3.55"
    compile "slimeknights:TConstruct:1.12.2-2.13.0.190"

    compile fg.deobf('com.sk89q.worldedit:worldedit-core:6.1.4-SNAPSHOT') {
        exclude group: 'com.google.code.gson'
        exclude group: 'com.google.guava'
    }
}

mixin {
    add sourceSets.main, "misca.refmap.json"
}

sourceSets {
    main { output.resourcesDir = output.classesDir }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.mod_version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.mod_version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    from configurations.embed.collect {
        it.isDirectory() ? it : zipTree(it)
    }
    exclude "META-INF/*.SF"
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"

    manifest.attributes(
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'TweakOrder': 0,
            'MixinConfigs': 'mixins.misca.json',
            'FMLCorePlugin': 'msifeed.misca.mixins.MiscaDepsPlugin',
            'FMLCorePluginContainsFMLMod': 'true',
            'ForceLoadAsMod': 'true'
    )
}

jar.finalizedBy('reobfJar')